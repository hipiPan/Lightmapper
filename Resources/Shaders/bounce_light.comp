#version 450 core

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

struct Vertex {
    vec4 position;
    vec4 normal;
    vec2 uv0;
    vec2 uv1;
};

layout(set = 0, binding = 2000, std430) restrict readonly buffer Vertices {
    Vertex data[];
} vertices;

struct Triangle {
    uvec4 indices;
    vec4 min_bounds;
    vec4 max_bounds;
};

layout(set = 0, binding = 2001, std430) restrict readonly buffer Triangles {
    Triangle data[];
} triangles;

layout(set = 0, binding = 2002, std430) restrict readonly buffer GridIndices {
    uint data[];
} grid_indices;

layout(binding = 1000) uniform texture2D position_texture;
layout(binding = 1001) uniform texture2D normal_texture;
layout(binding = 1002) uniform utexture3D grid_texture;
layout(binding = 1003) uniform texture2D source_light_texture;
layout(binding = 2004, rgba32f) uniform image2D dest_light_texture;
layout(binding = 2005, rgba32f) uniform image2DArray sh_light_map;
layout(binding = 2006, rgba32f) uniform image2D bounce_accum_texture;
layout(binding = 3000) uniform sampler linear_sampler;
layout(binding = 3001) uniform sampler nearest_sampler;

layout(push_constant) uniform BakeParams {
    vec4 bound_size;
    vec4 to_cell_offset;
    vec4 to_cell_size;
    ivec2 atlas_size;
    float bias;
    uint ray_count;
    uint ray_count_per_iteration;
    uint light_count;
    uint grid_size;
    uint offset_x;
    uint offset_y;
    uint max_iterations;
    uint current_iterations;
    uint bounces;
} params;

struct Interaction {
    bool hit;
    float hit_dist;
    vec3 hit_position;
    uint tri_idx;
    vec3 barycentric;
};

const float PI = 3.14159265f;

uvec4 seed;

void InitRNG(vec2 p, int frame) {
    seed = uvec4(p, uint(frame), uint(p.x) + uint(p.y));
}

void pcg4d(inout uvec4 v) {
    v = v * 1664525u + 1013904223u;
    v.x += v.y * v.w; v.y += v.z * v.x; v.z += v.x * v.y; v.w += v.y * v.z;
    v = v ^ (v >> 16u);
    v.x += v.y * v.w; v.y += v.z * v.x; v.z += v.x * v.y; v.w += v.y * v.z;
}

float Rand() {
    pcg4d(seed); return float(seed.x) / float(0xffffffffu);
}

bool RayHitTriangle(vec3 from, vec3 dir, float max_dist, vec3 p0, vec3 p1, vec3 p2, out float r_distance, out vec3 r_barycentric) {
    const float EPSILON = 0.00001;
    const vec3 e0 = p1 - p0;
    const vec3 e1 = p0 - p2;
    vec3 triangle_normal = cross(e1, e0);

    float n_dot_dir = dot(triangle_normal, dir);

    if (abs(n_dot_dir) < EPSILON) {
        return false;
    }

    const vec3 e2 = (p0 - from) / n_dot_dir;
    const vec3 i = cross(dir, e2);

    r_barycentric.y = dot(i, e1);
    r_barycentric.z = dot(i, e0);
    r_barycentric.x = 1.0 - (r_barycentric.z + r_barycentric.y);
    r_distance = dot(triangle_normal, e2);

    return (r_distance > 0.0) && (r_distance < max_dist) && all(greaterThanEqual(r_barycentric, vec3(0.0)));
}

const uint RAY_MISS = 0;
const uint RAY_FRONT = 1;
const uint RAY_BACK = 2;
const uint RAY_ANY = 3;

uint TraceRay(vec3 p_from, vec3 p_to, out Interaction isect) {
    vec3 rel = p_to - p_from;
    float rel_len = length(rel);
    vec3 dir = normalize(rel);
    vec3 inv_dir = 1.0 / dir;
    vec3 from_cell = (p_from - params.to_cell_offset.xyz) * params.to_cell_size.xyz;
    vec3 to_cell = (p_to - params.to_cell_offset.xyz) * params.to_cell_size.xyz;
    vec3 rel_cell = to_cell - from_cell;
    ivec3 icell = ivec3(from_cell);
    ivec3 iendcell = ivec3(to_cell);
    vec3 dir_cell = normalize(rel_cell);
    vec3 delta = min(abs(1.0 / dir_cell), params.grid_size);
    ivec3 step = ivec3(sign(rel_cell));
    vec3 side = (sign(rel_cell) * (vec3(icell) - from_cell) + (sign(rel_cell) * 0.5) + 0.5) * delta;

    uint iters = 0;
    while (all(greaterThanEqual(icell, ivec3(0))) && all(lessThan(icell, ivec3(params.grid_size))) && iters < 1000) {
        uvec2 cell_data = texelFetch(usampler3D(grid_texture, nearest_sampler), icell, 0).xy;
        if (cell_data.x > 0) {
            uint hit = RAY_MISS;
            float best_distance = 1e20;

            for (uint i = 0; i < cell_data.x; i++) {
                uint tidx = grid_indices.data[cell_data.y + i];

                Triangle triangle = triangles.data[tidx];
                vec3 t0 = (triangle.min_bounds.xyz - p_from) * inv_dir;
                vec3 t1 = (triangle.max_bounds.xyz - p_from) * inv_dir;
                vec3 tmin = min(t0, t1), tmax = max(t0, t1);

                if (max(tmin.x, max(tmin.y, tmin.z)) > min(tmax.x, min(tmax.y, tmax.z))) {
                    continue;
                }

                vec3 vtx0 = vertices.data[triangle.indices.x].position.xyz;
                vec3 vtx1 = vertices.data[triangle.indices.y].position.xyz;
                vec3 vtx2 = vertices.data[triangle.indices.z].position.xyz;
                vec3 normal = normalize(cross((vtx0 - vtx1), (vtx0 - vtx2)));
                bool backface = dot(normal, dir) >= 0.0;
                float distance;
                vec3 barycentric;

                if (RayHitTriangle(p_from, dir, rel_len, vtx0, vtx1, vtx2, distance, barycentric)) {
                    if (!backface) {
                        distance = max(params.bias, distance - params.bias);
                    }

                    // 选择最优距离
                    if (distance < best_distance) {
                        hit = backface ? RAY_BACK : RAY_FRONT;
                        best_distance = distance;
                        isect.hit = true;
                        isect.hit_dist = distance;
                        // surface offset?
                        isect.hit_position = p_to + dir * distance;
                        isect.barycentric = barycentric;
                        isect.tri_idx = tidx;
                    }
                }
            }

            if (hit != RAY_MISS) {
                return hit;
            }
        }

        if (icell == iendcell) {
            break;
        }

        bvec3 mask = lessThanEqual(side.xyz, min(side.yzx, side.zxy));
        side += vec3(mask) * delta;
        icell += ivec3(vec3(mask)) * step;

        iters++;
    }

    isect.hit = false;
    return RAY_MISS;
}

#define LIGHT_TYPE_DIRECTIONAL 0
#define LIGHT_TYPE_OMNI 1
#define LIGHT_TYPE_SPOT 2

vec3 SampleOneLight(vec3 scatter_pos, uint light_index) {
    return vec3(0.0);
}

vec3 PathTrace(vec3 p_from, vec3 p_to) {
    vec3 rel = p_to - p_from;
    float ray_len = length(rel);
    vec3 ray_dir = normalize(rel);
    vec3 ray_origin = p_from;

    vec3 beta = vec3(1.0);
    vec3 radiance = vec3(0.0);

    for(uint bounce = 0; bounce < params.bounces; bounce++) {


        uint light_index = uint(Rand() * float(params.light_count));

        SampleOneLight(p_from, light_index);

        // RR

    }
    return vec3(0.0);
}

float RadicalInverse_VdC(uint bits) {
    (bits = ((bits << uint (16)) | (bits >> uint (16))));
    (bits = (((bits & 1431655765u) << uint (1)) | ((bits & 2863311530u) >> uint (1))));
    (bits = (((bits & 858993459u) << uint (2)) | ((bits & 3435973836u) >> uint (2))));
    (bits = (((bits & 252645135u) << uint (4)) | ((bits & 4042322160u) >> uint (4))));
    (bits = (((bits & 16711935u) << uint (8)) | ((bits & 4278255360u) >> uint (8))));
    return (float(bits) * 2.328306e-10);
}

vec2 Hammersley(uint i, uint N) {
    return vec2((float(i) / float(N)), RadicalInverse_VdC(i));
}

vec3 GenerateHemisphereDirection(uint i, uint N) {
    vec2 Xi = Hammersley(i, N);

    float noise1 = Xi.x;
    float noise2 = Xi.y * 2.0 * PI;

    return vec3(sqrt(noise1) * cos(noise2), sqrt(noise1) * sin(noise2), sqrt(1.0 - noise1));
}

void main() {
    ivec2 atlas_pos = ivec2(gl_GlobalInvocationID.xy) + ivec2(params.offset_x, params.offset_y);

    vec3 normal = texelFetch(sampler2D(normal_texture, linear_sampler), atlas_pos, 0).xyz;
    if (length(normal) < 0.3) {
        return;
    }

    vec3 position = texelFetch(sampler2D(position_texture, linear_sampler), atlas_pos, 0).xyz;
    position += sign(normal) * abs(position * 0.0002);

    InitRNG(atlas_pos, 0);

    vec3 v0 = abs(normal.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(0.0, 1.0, 0.0);
    vec3 tangent = normalize(cross(v0, normal));
    vec3 bitangent = normalize(cross(tangent, normal));
    mat3 normal_mat = mat3(tangent, bitangent, normal);

    vec4 sh_accum[4] = vec4[](
    vec4(0.0, 0.0, 0.0, 1.0),
    vec4(0.0, 0.0, 0.0, 1.0),
    vec4(0.0, 0.0, 0.0, 1.0),
    vec4(0.0, 0.0, 0.0, 1.0));

    sh_accum[0] = imageLoad(sh_light_map, ivec3(atlas_pos, 0));
    sh_accum[1] = imageLoad(sh_light_map, ivec3(atlas_pos, 1));
    sh_accum[2] = imageLoad(sh_light_map, ivec3(atlas_pos, 2));
    sh_accum[3] = imageLoad(sh_light_map, ivec3(atlas_pos, 3));

    vec3 light_average = vec3(0.0);
    float active_rays = 0.0;

    vec3 light_total = vec3(0.0);
    if (params.current_iterations == 0) {
        light_total = vec3(0.0);
    } else {
        vec4 accum = imageLoad(bounce_accum_texture, ivec2(atlas_pos));
        light_total = accum.rgb;
        active_rays += accum.a;
    }

    uint miss_count = 0;

    for (uint i = 0; i < params.ray_count_per_iteration; i++) {
        vec2 random_offset = vec2(Rand(), Rand());
        random_offset *= 0.00f;
        vec3 ray_dir = normal_mat * GenerateHemisphereDirection(uint(i) + (params.ray_count_per_iteration * params.current_iterations), uint(params.ray_count));
        vec3 light = vec3(0.0);
        Interaction isect;
        uint trace_result = TraceRay(position + ray_dir, position + ray_dir * length(params.bound_size.xyz), isect);
        if (trace_result == RAY_FRONT) {
            vec2 uv0 = vertices.data[triangles.data[isect.tri_idx].indices.x].uv1;
            vec2 uv1 = vertices.data[triangles.data[isect.tri_idx].indices.y].uv1;
            vec2 uv2 = vertices.data[triangles.data[isect.tri_idx].indices.z].uv1;
            vec2 uv = isect.barycentric.x * uv0 + isect.barycentric.y * uv1 + isect.barycentric.z * uv2;
            light = texture(sampler2D(source_light_texture, linear_sampler), uv).rgb;

            active_rays += 1.0;
        } else if (trace_result == RAY_BACK) {
            miss_count++;
            continue;
        } else if (trace_result == RAY_MISS) {
            // todo: env light
            miss_count++;
            continue;
        }

        light_average += light;

        float c[4] = float[](
            0.282095, //l0
            0.488603 * ray_dir.y, //l1n1
            0.488603 * ray_dir.z, //l1n0
            0.488603 * ray_dir.x //l1p1
        );

        for (uint j = 0; j < 4; j++) {
            sh_accum[j].rgb += light * c[j] * (8.0 / float(params.ray_count * 3));
        }
    }

    imageStore(sh_light_map, ivec3(atlas_pos, 0), sh_accum[0]);
    imageStore(sh_light_map, ivec3(atlas_pos, 1), sh_accum[1]);
    imageStore(sh_light_map, ivec3(atlas_pos, 2), sh_accum[2]);
    imageStore(sh_light_map, ivec3(atlas_pos, 3), sh_accum[3]);

    light_total += light_average;

    if (params.current_iterations == params.max_iterations - 1) {
        if (active_rays > 0) {
            light_total /= active_rays;
        }
        imageStore(dest_light_texture, ivec2(atlas_pos), vec4(light_total, 1.0));

        vec4 accum = imageLoad(sh_light_map, ivec3(atlas_pos, 0));
        accum.rgb += light_total;
        imageStore(sh_light_map, ivec3(atlas_pos, 0), accum);
    } else {
        imageStore(bounce_accum_texture, ivec2(atlas_pos), vec4(light_total, active_rays));
    }
}